/* *************************************************************************
 * NAME : dbo.UDP_LOTTE_PDSS_STATS
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2018-05-13 18:30:42.237
 *        Modify: 2018-05-13 18:30:42.237
 *        Backup: 20180521_1739
 ************************************************************************* */


CREATE PROC [DBO].[UDP_LOTTE_PDSS_STATS]
	@SHOP_CODE INT
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	/*
		TBL_EXTERNAL_ATTRIBUTE			-- (셋팅 속성)
		TBL_EXTERNAL_ATTRIBUTE_BACKUP	-- (셋팅 속성 삭제)
		TBL_EXTERNAL_AUTO_TARGET		-- (자동매칭 내역)
		TBL_EXTERNAL_CATEGORY			-- (전송 분류)
		TBL_EXTERNAL_MODELNO_LOG		-- (CM 속성 로그)
		TBL_EXTERNAL_SEND_TARGET_MODEL	-- (누적 전송 모델)
		TBL_EXTERNAL_SEND_TARGET_PLNO	-- (누적 전송 EP)
		TBL_EXTERNAL_DENIED_PLNO		-- (CM 매칭 불가 내역)
		
		TBL_EXTERNAL_REQUEST			-- (GS 요청 내역)
		TBL_EXTERNAL_STATS_ATTRIBUTE	-- (통계 - ENURI 분류 기준)
		TBL_EXTERNAL_STATS_ENURI		-- (통계 - ENURI 분류 기준)
		TBL_EXTERNAL_STATS_GS			-- (통계 - GS 분류 기준)
		
	*/
	
	DELETE /* UDP_LOTTE_PDSS_STATS:01 */ FROM TBL_EXTERNAL_STATS_ENURI	WHERE SHOP_CODE = @SHOP_CODE AND YYYYMMDD = CONVERT(CHAR(8), DATEADD(D, -1, GETDATE()), 112)
	DELETE /* UDP_LOTTE_PDSS_STATS:02 */ FROM TBL_EXTERNAL_STATS_GS		WHERE SHOP_CODE = @SHOP_CODE AND YYYYMMDD = CONVERT(CHAR(8), DATEADD(D, -1, GETDATE()), 112)
	
	-----------------------------------------------------------------------------------
	-- ENURI CATEGORY 기준
	-----------------------------------------------------------------------------------
	-- PL_NO 기준 STATS (매일 누적)
	INSERT /* UDP_LOTTE_PDSS_STATS:03 */ INTO TBL_EXTERNAL_STATS_ENURI(SHOP_CODE, YYYYMMDD, CATEGORY, SEND_CNT, MATCHING_CNT, DENIED_CNT)
	SELECT @SHOP_CODE, CONVERT(CHAR(8), DATEADD(D, -1, GETDATE()), 112), LEFT(A.CATEGORY, 6), COUNT(A.PL_NO), SUM(A.MATCHING_YN), SUM(A.DENIED_YN)
	FROM (
		SELECT A.PL_NO, RTRIM(ISNULL(A.PL_CATEGORY, '')) CATEGORY
			 , CASE WHEN B.PL_NO IS NOT NULL THEN 1 ELSE 0 END MATCHING_YN
			 , CASE WHEN C.PL_NO IS NOT NULL THEN 1 ELSE 0 END DENIED_YN
		FROM PRICELIST A
			LEFT JOIN TBL_PDSS_LOTTE_SEND_EP B ON B.PL_VCODE = @SHOP_CODE AND A.PL_NO = B.PL_NO
			LEFT JOIN TBL_EXTERNAL_DENIED_PLNO C ON C.SHOP_CODE = @SHOP_CODE AND A.PL_NO = C.PL_NO
		WHERE A.PL_VCODE = @SHOP_CODE AND A.PL_STATUS = '4' AND A.PL_SRVFLAG = '4'
	) A
	GROUP BY LEFT(A.CATEGORY, 6)
	OPTION (MAXDOP 1)
	
	-----------------------------------------------------------------------------------
	-- GS CATEGORY 기준
	-----------------------------------------------------------------------------------
	-- PL_NO 기준 STATS (매일 누적)
	INSERT /* UDP_LOTTE_PDSS_STATS:04 */ INTO TBL_EXTERNAL_STATS_GS(SHOP_CODE, YYYYMMDD, CATEGORY, SEND_CNT, MATCHING_CNT, DENIED_CNT)
	SELECT @SHOP_CODE, CONVERT(CHAR(8), DATEADD(D, -1, GETDATE()), 112), A.CATEGORY, COUNT(A.PL_NO), SUM(A.MATCHING_YN), SUM(A.DENIED_YN)
	FROM (
		SELECT A.PL_NO, RTRIM(ISNULL(CASE WHEN CHARINDEX('_', A.PL_MULTICOMMENT) > 0 THEN SUBSTRING(A.PL_MULTICOMMENT, 1, CHARINDEX('_', A.PL_MULTICOMMENT) - 1) ELSE A.PL_MULTICOMMENT END, '')) CATEGORY
			 , CASE WHEN B.PL_NO IS NOT NULL THEN 1 ELSE 0 END MATCHING_YN
			 , CASE WHEN C.PL_NO IS NOT NULL THEN 1 ELSE 0 END DENIED_YN
		FROM PRICELIST A
			LEFT JOIN TBL_PDSS_LOTTE_SEND_EP B ON B.PL_VCODE = @SHOP_CODE AND A.PL_NO = B.PL_NO
			LEFT JOIN TBL_EXTERNAL_DENIED_PLNO C ON C.SHOP_CODE = @SHOP_CODE AND A.PL_NO = C.PL_NO
		WHERE A.PL_VCODE = @SHOP_CODE AND A.PL_STATUS = '4' AND A.PL_SRVFLAG = '4'
	) A
	GROUP BY A.CATEGORY
	OPTION (MAXDOP 1)
END

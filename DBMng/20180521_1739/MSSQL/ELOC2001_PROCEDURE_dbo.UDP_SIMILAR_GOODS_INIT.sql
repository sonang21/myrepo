/* *************************************************************************
 * NAME : dbo.UDP_SIMILAR_GOODS_INIT
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2018-02-01 11:29:45.173
 *        Modify: 2018-05-03 17:23:35.677
 *        Backup: 20180521_1739
 ************************************************************************* */


CREATE PROC [DBO].[UDP_SIMILAR_GOODS_INIT]
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	CREATE TABLE #DELETE_CATALOG(G_MODELNO INT PRIMARY KEY, G_MODELNM VARCHAR(500), G_FACTORY VARCHAR(500), G_CATEGORY VARCHAR(12))
	CREATE TABLE #TEMP(G_MODELNO INT)
	
	IF NOT EXISTS(SELECT /* UDP_SIMILAR_GOODS_INIT:01 */ TOP 1 1 FROM TBL_DUPCHECK_MODEL_LOG WHERE START_DATE = CONVERT(VARCHAR(10), GETDATE(), 112) AND CA_CODE = '00')
	BEGIN
		INSERT /* UDP_SIMILAR_GOODS_INIT:02 */ INTO TBL_DUPCHECK_MODEL_LOG(START_DATE, CA_CODE, SUM_START_DATE, MERGE_START_DATE)
		SELECT CONVERT(VARCHAR(10), GETDATE(), 112), '00', GETDATE(), GETDATE()
	END
	
	-------------------------------------------------------------------------------------
	-------------------------------------------------------------------------------------
	-- 임시 삭제
	SELECT /* UDP_SIMILAR_GOODS_INIT:03 */ A.G_MODELNO
	INTO #TARGET_CATALOG
	FROM GOODS A
		LEFT JOIN GOODS_SUM B ON A.G_MODELNO = B.G_MODELNO
	WHERE NOT (G_MODELNM = '모델삭제' OR G_CATEGORY = '00000000')
	  AND G_CONSTRAIN = '5' AND ISNULL(G_MALLCNT, 0) = 0
	  AND NOT EXISTS(SELECT TOP 1 1 FROM PRICELIST WHERE PL_MODELNO = A.G_MODELNO)
	OPTION (MAXDOP 1)
	
	WHILE @@ROWCOUNT > 0
	BEGIN
		SET ROWCOUNT 500;
		TRUNCATE /* UDP_SIMILAR_GOODS_INIT:04 */ TABLE #TEMP
		DELETE /* UDP_SIMILAR_GOODS_INIT:05 */ FROM #TARGET_CATALOG
		OUTPUT DELETED.G_MODELNO INTO #TEMP
		SET ROWCOUNT 0;
		
		-- 모델 삭제
		UPDATE /* UDP_SIMILAR_GOODS_INIT:06 */ GOODS
		SET G_FLAG = '1', G_MODELNM = '모델삭제', G_CATEGORY = '00000000'
		OUTPUT DELETED.G_MODELNO, DELETED.G_MODELNM, DELETED.G_FACTORY, DELETED.G_CATEGORY INTO #DELETE_CATALOG
		WHERE G_MODELNO IN (SELECT G_MODELNO FROM #TEMP)
		OPTION (MAXDOP 1)
		
		-- 삭제처리 로그
		INSERT /* UDP_SIMILAR_GOODS_INIT:07 */ INTO GOODS_DELETE_MODELNO (G_MODELNO, G_MODELNM, G_FACTORY, G_CATEGORY, MM_ID)
		SELECT G_MODELNO, G_MODELNM, G_FACTORY, G_CATEGORY, 'Lab_batch'
		FROM #DELETE_CATALOG
		WHERE G_MODELNO IN (SELECT G_MODELNO FROM #TEMP A WHERE NOT EXISTS(SELECT TOP 1 1 FROM GOODS_DELETE_MODELNO B WHERE B.G_MODELNO = A.G_MODELNO))
		OPTION (MAXDOP 1)
	END
	-------------------------------------------------------------------------------------
	-------------------------------------------------------------------------------------
	
	DECLARE @MODELNO INT
	DECLARE FETCH_CURSOR CURSOR FOR
	SELECT /* UDP_SIMILAR_GOODS_INIT:08 */ G_MODELNO
	FROM #DELETE_CATALOG
	
	OPEN FETCH_CURSOR
	
	FETCH NEXT FROM FETCH_CURSOR INTO @MODELNO
	WHILE @@FETCH_STATUS = 0
	BEGIN
		EXEC UDP_GOODS_INIT @MODELNO, ''	-- 모델삭제 프로시저 (ELOC & ORACLE)
		FETCH NEXT FROM FETCH_CURSOR INTO @MODELNO
	END
	
	CLOSE FETCH_CURSOR
	DEALLOCATE FETCH_CURSOR
	
	UPDATE /* UDP_SIMILAR_GOODS_INIT:09 */ TBL_DUPCHECK_MODEL_LOG
	SET AUTO_MODEL_CNT = AUTO_MODEL_CNT + (SELECT COUNT(*) FROM #DELETE_CATALOG)
	  , SUM_END_DATE = GETDATE(), MERGE_END_DATE = GETDATE()
	WHERE START_DATE = CONVERT(VARCHAR(10), GETDATE(), 112) AND CA_CODE = '00'
END

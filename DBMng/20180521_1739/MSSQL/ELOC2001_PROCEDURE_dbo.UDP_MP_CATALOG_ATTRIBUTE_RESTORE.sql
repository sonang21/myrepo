/* *************************************************************************
 * NAME : dbo.UDP_MP_CATALOG_ATTRIBUTE_RESTORE
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2016-05-31 17:20:09.59
 *        Modify: 2018-05-03 17:23:34.45
 *        Backup: 20180521_1739
 ************************************************************************* */


CREATE PROC [DBO].[UDP_MP_CATALOG_ATTRIBUTE_RESTORE]
	@MM_ID VARCHAR(20)
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	DECLARE @G_MODELNO INT
	DECLARE @ATTRIBUTE_ID INT
	DECLARE @ATTRIBUTE_ELEMENT_ID INT
	DECLARE @ATTRIBUTE_VALUE VARCHAR(500)
	DECLARE @DEL_YN CHAR(1)
	DECLARE @STATUS CHAR(1)
	
	CREATE TABLE #LOG_GOODS(G_MODELNO INT)
	
	DECLARE FETCH_CURSOR CURSOR FOR
	SELECT G_MODELNO, ATTRIBUTE_ID, ATTRIBUTE_ELEMENT_ID, ATTRIBUTE_VALUE, DEL_YN, STATUS
	FROM (
		SELECT RANK() OVER(PARTITION BY MM_ID, G_MODELNO, ATTRIBUTE_ID, ATTRIBUTE_ELEMENT_ID, (CASE WHEN STATUS = 'I' THEN 'U' ELSE STATUS END) ORDER BY REGDATE) NRANK
			 , G_MODELNO, ATTRIBUTE_ID, ATTRIBUTE_ELEMENT_ID, ATTRIBUTE_VALUE, DEL_YN, STATUS, REGDATE
		FROM GOODS_CATALOG_ATTRIBUTE_BACKUP
		WHERE MM_ID = @MM_ID) A
	WHERE NRANK = 1
	ORDER BY REGDATE DESC

	OPEN FETCH_CURSOR

	FETCH NEXT FROM FETCH_CURSOR INTO @G_MODELNO, @ATTRIBUTE_ID, @ATTRIBUTE_ELEMENT_ID, @ATTRIBUTE_VALUE, @DEL_YN, @STATUS
	WHILE @@FETCH_STATUS = 0
	BEGIN
		IF @STATUS = 'E'			-- E : 加己盔 昏力
		BEGIN
			UPDATE GOODS_ATTRIBUTE_ELEMENT
			SET DEL_YN = @DEL_YN, UPD_DATE = GETDATE(), UPD_OPRT = @MM_ID, INS_DATE = (CASE WHEN @DEL_YN = 'N' THEN GETDATE() ELSE INS_DATE END)
			WHERE ATTRIBUTE_ID = @ATTRIBUTE_ID AND ATTRIBUTE_ELEMENT_ID = @ATTRIBUTE_ELEMENT_ID AND DEL_YN <> @DEL_YN
		END
		ELSE IF @STATUS = 'A'		-- A : 加己 昏力
		BEGIN
			UPDATE GOODS_ATTRIBUTE
			SET DEL_YN = @DEL_YN, UPD_DATE = GETDATE(), UPD_OPRT = @MM_ID, INS_DATE = (CASE WHEN @DEL_YN = 'N' THEN GETDATE() ELSE INS_DATE END)
			WHERE ATTRIBUTE_ID = @ATTRIBUTE_ID AND DEL_YN <> @DEL_YN
		END
		ELSE IF @STATUS = 'C'		-- C : 盒幅加己 昏力 
		BEGIN
			IF NOT EXISTS (SELECT 1 FROM GOODS_CATEGORY_ATTRIBUTE WHERE CATEGORY = @ATTRIBUTE_VALUE AND ATTRIBUTE_ID = @ATTRIBUTE_ID)
			BEGIN
				INSERT INTO GOODS_CATEGORY_ATTRIBUTE(CATEGORY, ATTRIBUTE_ID, DISPLAY_ORDER, INS_DATE, INS_OPRT)
				SELECT TOP 1 @ATTRIBUTE_VALUE, @ATTRIBUTE_ID, @ATTRIBUTE_ELEMENT_ID, GETDATE(), @MM_ID
			END
		END
		ELSE						-- I : 眠啊, U : 昏力
		BEGIN
			IF NOT EXISTS (SELECT 1 FROM GOODS_CATALOG_ATTRIBUTE WHERE G_MODELNO = @G_MODELNO AND ATTRIBUTE_ID = @ATTRIBUTE_ID AND ATTRIBUTE_ELEMENT_ID = @ATTRIBUTE_ELEMENT_ID)
			BEGIN
				INSERT INTO GOODS_CATALOG_ATTRIBUTE(G_MODELNO, ATTRIBUTE_ID, ATTRIBUTE_ELEMENT_ID, ATTRIBUTE_VALUE, INS_DATE, INS_OPRT, DEL_YN, UPD_DATE, UPD_OPRT)
				OUTPUT INSERTED.G_MODELNO
					INTO #LOG_GOODS
				SELECT TOP 1 @G_MODELNO, @ATTRIBUTE_ID, @ATTRIBUTE_ELEMENT_ID, @ATTRIBUTE_VALUE, GETDATE(), @MM_ID, @DEL_YN, GETDATE(), @MM_ID
			END
			
			UPDATE GOODS_CATALOG_ATTRIBUTE
			SET DEL_YN = @DEL_YN, UPD_DATE = GETDATE(), UPD_OPRT = @MM_ID, ATTRIBUTE_VALUE = @ATTRIBUTE_VALUE, INS_DATE = (CASE WHEN @DEL_YN = 'N' THEN GETDATE() ELSE INS_DATE END)
			OUTPUT DELETED.G_MODELNO
				INTO #LOG_GOODS
			WHERE G_MODELNO = @G_MODELNO AND ATTRIBUTE_ID = @ATTRIBUTE_ID AND ATTRIBUTE_ELEMENT_ID = @ATTRIBUTE_ELEMENT_ID
			  AND (DEL_YN <> @DEL_YN OR ATTRIBUTE_VALUE <> @ATTRIBUTE_VALUE)
		END

		FETCH NEXT FROM FETCH_CURSOR INTO @G_MODELNO, @ATTRIBUTE_ID, @ATTRIBUTE_ELEMENT_ID, @ATTRIBUTE_VALUE, @DEL_YN, @STATUS	
	END

	CLOSE FETCH_CURSOR
	DEALLOCATE FETCH_CURSOR

	UPDATE GOODS
	SET G_FLAG = '1'
	WHERE G_MODELNO IN (SELECT G_MODELNO FROM #LOG_GOODS)

	DELETE FROM GOODS_CATALOG_ATTRIBUTE_BACKUP
	WHERE MM_ID = @MM_ID
END

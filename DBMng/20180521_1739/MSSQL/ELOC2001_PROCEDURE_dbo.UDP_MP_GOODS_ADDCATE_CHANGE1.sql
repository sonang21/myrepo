/* *************************************************************************
 * NAME : dbo.UDP_MP_GOODS_ADDCATE_CHANGE1
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2016-04-05 20:20:49.267
 *        Modify: 2018-05-03 17:23:34.78
 *        Backup: 20180521_1739
 ************************************************************************* */


CREATE PROCEDURE [DBO].[UDP_MP_GOODS_ADDCATE_CHANGE1]
	@PRE_CATE VARCHAR(12),
	@NEW_CATE VARCHAR(12),
	@MM_ID VARCHAR(12),
	@MM_NM VARCHAR(12)
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	DECLARE @AFF_ROWS INT
	SET @AFF_ROWS = 0
	
	IF (@NEW_CATE = '')
		-- 추가분류 삭제
		BEGIN
			UPDATE GOODS
			SET G_FLAG = '1'
			WHERE G_MODELNO IN (
					SELECT GA_MODELNO FROM GOODS_ADDCATE
					WHERE GA_CATEGORY = @PRE_CATE AND GA_FLAG <> '3')
			
			UPDATE GOODS_ADDCATE
			SET GA_FLAG = '3'
			WHERE GA_CATEGORY = @PRE_CATE AND GA_FLAG <> '3'
			
			SET @AFF_ROWS = @@ROWCOUNT
		END
	ELSE
		-- 추가분류 일괄변경
		BEGIN
			UPDATE GOODS
			SET G_FLAG = '1'
			WHERE G_MODELNO IN (
					SELECT GA_MODELNO FROM GOODS_ADDCATE
					WHERE GA_FLAG <> '3' AND GA_CATEGORY = @PRE_CATE)
			
			--@PRE_CATE, @NEW_CATE 두분류 모두 가지고 있으면 @PRE_CATE '3'		
			UPDATE GOODS_ADDCATE
			SET GA_FLAG = '3'
			WHERE GA_MODELNO IN (
					SELECT GA_MODELNO FROM GOODS_ADDCATE
					WHERE GA_FLAG <> '3' AND GA_CATEGORY = @PRE_CATE) AND
				  GA_MODELNO IN (
					SELECT GA_MODELNO FROM GOODS_ADDCATE
					WHERE GA_FLAG <> '3' AND GA_CATEGORY = @NEW_CATE) AND
				  GA_CATEGORY = @PRE_CATE
			
			UPDATE GOODS_ADDCATE
			SET GA_CATEGORY = @NEW_CATE
			WHERE GA_FLAG <> '3' AND GA_CATEGORY = @PRE_CATE
			
			SET @AFF_ROWS = @@ROWCOUNT
		END
	
	IF (@AFF_ROWS > 0)
		BEGIN
			-- 분류변경 로그
			INSERT INTO LOGDB.DBO.TBL_CATE_CHG_LOG (DEL_CATE, NEW_CATE, MM_ID, MM_NM, CHG_FLAG, REGDATE)
			SELECT @PRE_CATE, CASE WHEN @NEW_CATE = '' THEN @PRE_CATE ELSE @NEW_CATE END, @MM_ID, @MM_NM, CASE WHEN @NEW_CATE = '' THEN '2' ELSE '1' END, GETDATE()
		END
END

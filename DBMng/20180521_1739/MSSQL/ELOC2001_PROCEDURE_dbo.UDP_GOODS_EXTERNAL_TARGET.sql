/* *************************************************************************
 * NAME : dbo.UDP_GOODS_EXTERNAL_TARGET
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2017-02-07 10:01:55.073
 *        Modify: 2018-05-03 17:23:35.997
 *        Backup: 20180521_1739
 ************************************************************************* */


CREATE PROC [DBO].[UDP_GOODS_EXTERNAL_TARGET]
	@SHOP_CODE INT
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	/*
		TBL_EXTERNAL_ATTRIBUTE			-- (셋팅 속성)
		TBL_EXTERNAL_ATTRIBUTE_BACKUP	-- (셋팅 속성 삭제)
		TBL_EXTERNAL_AUTO_TARGET		-- (자동매칭 내역)
		TBL_EXTERNAL_CATEGORY			-- (전송 분류)
		TBL_EXTERNAL_MODELNO_LOG		-- (CM 속성 로그)
		TBL_EXTERNAL_SEND_TARGET_MODEL	-- (누적 전송 모델)
		TBL_EXTERNAL_SEND_TARGET_PLNO	-- (누적 전송 EP)
		TBL_EXTERNAL_DENIED_PLNO		-- (CM 매칭 불가 내역)
	*/
	
	IF DATEPART(WEEKDAY, GETDATE()) <> 1 AND DATEPART(WEEKDAY, GETDATE()) <> 2
	BEGIN
		-- 자동매칭 일별 20,000개씩 OPEN
		SET ROWCOUNT 20000;
		DELETE FROM TBL_EXTERNAL_AUTO_TARGET
		SET ROWCOUNT 0;
	END
	
	-- 삭제 데이터 처리
	DELETE FROM TBL_EXTERNAL_SEND_TARGET_PLNO
	OUTPUT DELETED.YYYYMMDD, DELETED.SHOP_CODE, DELETED.PL_NO, DELETED.PL_MODELNO, DELETED.REGDATE, GETDATE(), 'D' INTO TBL_EXTERNAL_SEND_TARGET_PLNO_BACKUP
	FROM TBL_EXTERNAL_SEND_TARGET_PLNO A
		LEFT JOIN PRICELIST B ON B.PL_NO = A.PL_NO
	WHERE SHOP_CODE = @SHOP_CODE AND (B.PL_NO IS NULL OR NOT(PL_STATUS = '4' AND PL_SRVFLAG = '4'))
	OPTION (MAXDOP 1)
	
	----------------------------------------------------------
	-- 기전송 데이터 수정
	UPDATE TBL_EXTERNAL_SEND_TARGET_PLNO
	SET PL_MODELNO = B.PL_MODELNO
	OUTPUT DELETED.YYYYMMDD, DELETED.SHOP_CODE, DELETED.PL_NO, DELETED.PL_MODELNO, DELETED.REGDATE, GETDATE(), 'U' INTO TBL_EXTERNAL_SEND_TARGET_PLNO_BACKUP
	FROM TBL_EXTERNAL_SEND_TARGET_PLNO A
		INNER JOIN PRICELIST B ON B.PL_NO = A.PL_NO
	WHERE SHOP_CODE = @SHOP_CODE AND A.PL_MODELNO <> B.PL_MODELNO
	OPTION (MAXDOP 1)
	
	----------------------------------------------------------
	-- 신규(당일) 전송 대상 상품
	----------------------------------------------------------
	INSERT INTO TBL_EXTERNAL_SEND_TARGET_PLNO(YYYYMMDD, SHOP_CODE, PL_NO, PL_MODELNO)
	SELECT DISTINCT CONVERT(CHAR(8), DATEADD(D, -1, GETDATE()), 112), PL_VCODE, PL_NO, PL_MODELNO
	FROM PRICELIST
	WHERE PL_VCODE = @SHOP_CODE AND PL_MODELNO NOT IN (0, -100, -200, -300) AND PL_STATUS = '4' AND PL_SRVFLAG = '4'
	  AND NOT EXISTS (SELECT TOP 1 1 FROM TBL_EXTERNAL_AUTO_TARGET WHERE PL_NO = PRICELIST.PL_NO)		-- 자동매칭
	  AND NOT EXISTS (SELECT TOP 1 1 FROM TBL_EXTERNAL_SEND_TARGET_PLNO WHERE SHOP_CODE = @SHOP_CODE AND PL_NO = PRICELIST.PL_NO)	-- 기전송
	  AND LTRIM(RTRIM(ISNULL(PL_MULTICOMMENT, ''))) IN (SELECT CATEGORY_NM FROM TBL_EXTERNAL_CATEGORY WHERE SHOP_CODE = @SHOP_CODE AND SENDING_YN = 1)			-- 전송 분류
	OPTION (MAXDOP 1)
	
	----------------------------------------------------------
	-- 누적 전송 모델(최근 누적 전송) -- MALLCNT 집계 오전 3:10 시작
	----------------------------------------------------------
	TRUNCATE TABLE TBL_EXTERNAL_SEND_TARGET_MODEL
	INSERT INTO TBL_EXTERNAL_SEND_TARGET_MODEL(YYYYMMDD, SHOP_CODE, MODELNO, CATEGORY, ATTRIBUTE_CNT, S_CATEGORY, REGDATE)
	SELECT DISTINCT MIN(A.YYYYMMDD), MIN(A.SHOP_CODE), A.PL_MODELNO, MIN(B.G_CATEGORY), COUNT(DISTINCT C.ATTRIBUTE_ID), MIN(C.CATEGORY), MAX(UPD_DATE)
	FROM TBL_EXTERNAL_SEND_TARGET_PLNO A
		INNER JOIN GOODS B ON B.G_MODELNO = PL_MODELNO
		INNER JOIN TBL_EXTERNAL_ATTRIBUTE C ON C.SHOP_CODE = @SHOP_CODE AND C.CATEGORY = LEFT(G_CATEGORY, LEN(C.CATEGORY)) AND C.ATTRIBUTE_ID > 0
		INNER JOIN GOODS_CATEGORY_ATTRIBUTE D ON D.ATTRIBUTE_ID = C.ATTRIBUTE_ID AND D.CATEGORY IN (SELECT TOP 1 CATEGORY FROM GOODS_CATEGORY_ATTRIBUTE WHERE CATEGORY = LEFT(G_CATEGORY, 4) OR CATEGORY = LEFT(G_CATEGORY, 6) OR CATEGORY = LEFT(G_CATEGORY, 8) ORDER BY LEN(CATEGORY) DESC)
		INNER JOIN GOODS_CATALOG_ATTRIBUTE E ON E.G_MODELNO = B.G_MODELNO AND E.ATTRIBUTE_ID = C.ATTRIBUTE_ID AND DEL_YN = 'N'
	WHERE A.SHOP_CODE = @SHOP_CODE AND A.PL_MODELNO > 0
	GROUP BY A.PL_MODELNO
	OPTION (MAXDOP 1)
	
	----------------------------------------------------------
	-- 미입력 리스트
	----------------------------------------------------------
	TRUNCATE TABLE TBL_EXTERNAL_NON_TARGET
	INSERT INTO TBL_EXTERNAL_NON_TARGET(SHOP_CODE, MODELNO)
	SELECT DISTINCT @SHOP_CODE, B.G_MODELNO
	FROM TBL_EXTERNAL_SEND_TARGET_PLNO A
		INNER JOIN GOODS B ON B.G_MODELNO = PL_MODELNO
		LEFT JOIN TBL_EXTERNAL_SEND_TARGET_MODEL C ON C.SHOP_CODE = @SHOP_CODE AND MODELNO = PL_MODELNO
	WHERE A.SHOP_CODE = @SHOP_CODE AND A.PL_MODELNO > 0 AND MODELNO IS NULL
	OPTION (MAXDOP 1)
	
	----------------------------------------------------------
	-- 매칭 불가
	----------------------------------------------------------
	-- 오늘 기준 전체(누적) 불가 상품
	SELECT DISTINCT A.PL_NO, PL_CATEGORY, PL_GOODSCODE, PL_GOODSNM, PL_NOTE
	INTO #NEW_DENIED_PLNO
	FROM TBL_EXTERNAL_SEND_TARGET_PLNO A
		INNER JOIN PRICELIST B ON B.PL_NO = A.PL_NO
	WHERE SHOP_CODE = @SHOP_CODE AND A.PL_MODELNO < 0 AND A.PL_MODELNO NOT IN (0, -100, -200, -300)
	OPTION (MAXDOP 1)
	
	-- 변경 데이터 삭제처리
	DELETE FROM TBL_EXTERNAL_DENIED_PLNO
	WHERE SHOP_CODE = @SHOP_CODE
	  AND NOT EXISTS (SELECT TOP 1 1 FROM #NEW_DENIED_PLNO WHERE PL_NO = TBL_EXTERNAL_DENIED_PLNO.PL_NO)		-- 신규(당일) 데이터
	
	-- 기전송 데이터 수정
	UPDATE TBL_EXTERNAL_DENIED_PLNO
	SET PL_CATEGORY = B.PL_CATEGORY, PL_GOODSNM = B.PL_GOODSNM, PL_NOTE = B.PL_NOTE
	FROM TBL_EXTERNAL_DENIED_PLNO A
		INNER JOIN #NEW_DENIED_PLNO B ON B.PL_NO = A.PL_NO
	WHERE SHOP_CODE = @SHOP_CODE
	OPTION (MAXDOP 1)
	
	-- 신규(당일) 데이터만 추가
	INSERT INTO TBL_EXTERNAL_DENIED_PLNO(SHOP_CODE, YYYYMMDD, PL_NO, PL_CATEGORY, PL_GOODSCODE, PL_GOODSNM, PL_NOTE)
	SELECT @SHOP_CODE, CONVERT(CHAR(8), DATEADD(D, -1, GETDATE()), 112), PL_NO, PL_CATEGORY, PL_GOODSCODE, PL_GOODSNM, PL_NOTE
	FROM #NEW_DENIED_PLNO
	WHERE NOT EXISTS (SELECT TOP 1 1 FROM TBL_EXTERNAL_DENIED_PLNO WHERE SHOP_CODE = @SHOP_CODE AND PL_NO = #NEW_DENIED_PLNO.PL_NO)	-- 기전송
	
	DROP TABLE #NEW_DENIED_PLNO
	
	EXEC UDP_GOODS_EXTERNAL_STATS @SHOP_CODE
END

/* *************************************************************************
 * NAME : dbo.UDP_MP_GOODS_CATALOG_ATTRIBUTE_SPEC
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2016-04-05 20:26:34.873
 *        Modify: 2018-05-03 17:23:35.373
 *        Backup: 20180521_1739
 ************************************************************************* */


CREATE PROC [DBO].[UDP_MP_GOODS_CATALOG_ATTRIBUTE_SPEC]
	@MODELNO INT
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	DECLARE @CATEGORY VARCHAR(12)
	SELECT @CATEGORY = G_CATEGORY
	FROM GOODS
	WHERE G_MODELNO = @MODELNO;
	
	SELECT B.ATTRIBUTE_ID
		 , GALLERY_ATTRIBUTE_NM
		 , USE_CLASS_CODE
		 , GROUP_ATTRIBUTE_ID
		 , GROUP_CHAR
		 , IS_GALLERY_DISPLAY
		 , C.ATTRIBUTE_ELEMENT_ID
		 , ATTRIBUTE_ELEMENT
		 , ATTRIBUTE_VALUE
		 , IS_DISPLAY
		 , B.IS_BOLD ATTRIBUTE_BOLD
		 , C.IS_BOLD ELEMENT_BOLD
		 , CASE WHEN CHARINDEX('_', MANAGE_ATTRIBUTE_NM) > 0 THEN SUBSTRING(MANAGE_ATTRIBUTE_NM, 1, CHARINDEX('_', MANAGE_ATTRIBUTE_NM) - 1) ELSE MANAGE_ATTRIBUTE_NM END GROUPNAME
	FROM GOODS_CATALOG_ATTRIBUTE A
		INNER JOIN GOODS_ATTRIBUTE B ON A.ATTRIBUTE_ID = B.ATTRIBUTE_ID
		INNER JOIN GOODS_ATTRIBUTE_ELEMENT C ON A.ATTRIBUTE_ID = C.ATTRIBUTE_ID AND A.ATTRIBUTE_ELEMENT_ID = C.ATTRIBUTE_ELEMENT_ID
		INNER JOIN GOODS_CATEGORY_ATTRIBUTE D ON A.ATTRIBUTE_ID = D.ATTRIBUTE_ID AND D.CATEGORY IN (SELECT TOP 1 CATEGORY FROM GOODS_CATEGORY_ATTRIBUTE WHERE CATEGORY = LEFT(@CATEGORY, 4) OR CATEGORY = LEFT(@CATEGORY, 6) OR CATEGORY = LEFT(@CATEGORY, 8) ORDER BY LEN(CATEGORY) DESC)
	WHERE A.DEL_YN = 'N'
	  AND B.DEL_YN = 'N'
	  AND C.DEL_YN = 'N'
	  AND G_MODELNO = @MODELNO
	  AND USE_CLASS_CODE < '4'
	  AND LTRIM(RTRIM(ISNULL(CASE WHEN USE_CLASS_CODE <> '2' THEN ATTRIBUTE_ELEMENT ELSE ATTRIBUTE_VALUE END, ''))) <> ''
	ORDER BY D.DISPLAY_ORDER
	       , CASE WHEN ISNULL(GROUP_ATTRIBUTE_ID, 0) = B.ATTRIBUTE_ID THEN '0' ELSE '1' END
		   , GALLERY_ATTRIBUTE_NM
		   , C.DISPLAY_ORDER
		   , CASE WHEN USE_CLASS_CODE <> '2' THEN ATTRIBUTE_ELEMENT ELSE ATTRIBUTE_VALUE END
END

/* *************************************************************************
 * NAME : dbo.UDP_MP_TBL_DUPCHECK_MODEL
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2017-04-27 11:13:11.587
 *        Modify: 2018-05-13 16:22:28.44
 *        Backup: 20180521_1739
 ************************************************************************* */


CREATE PROC [DBO].[UDP_MP_TBL_DUPCHECK_MODEL]
	@CA_CODE VARCHAR(12)
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	SELECT /* UDP_MP_TBL_DUPCHECK_MODEL:01 */ INDEXER, CA_CODE, MODELNO, CONSTRAIN, JTYPE, JOB_FLAG, INDEXER_TYPE, DENSE_RANK() OVER(ORDER BY CA_CODE, JTYPE, CASE WHEN INDEXER_TYPE = 'I' THEN 0 WHEN INDEXER_TYPE = 'T' THEN 1 WHEN INDEXER_TYPE = 'M' THEN 2 END, INDEXER) GROUPNO
	INTO #DUPCHECK
	FROM TBL_DUPCHECK_MODEL
	WHERE CA_CODE = @CA_CODE AND JTYPE IN ('1', '3') AND JOB_FLAG = '0' AND INDEXER_TYPE IN ('I', 'T', 'M')
	OPTION (MAXDOP 1)
	CREATE /* UDP_MP_TBL_DUPCHECK_MODEL:02 */ NONCLUSTERED INDEX NCL_DUPCHECK_01 ON #DUPCHECK(MODELNO ASC)
	CREATE /* UDP_MP_TBL_DUPCHECK_MODEL:03 */ NONCLUSTERED INDEX NCL_DUPCHECK_02 ON #DUPCHECK(GROUPNO ASC)
	
	SELECT /* UDP_MP_TBL_DUPCHECK_MODEL:04 */ MODELNO
	INTO #DUPCHECK_DUP
	FROM TBL_DUPCHECK_MODEL
	WHERE CA_CODE = @CA_CODE AND JTYPE IN ('1', '3') AND JOB_FLAG = '0' AND INDEXER_TYPE IN ('I', 'T', 'M')
	GROUP BY MODELNO
	HAVING COUNT(DISTINCT INDEXER_TYPE) > 1
	OPTION (MAXDOP 1)
	
	------------------------------------------------------------------------
	DECLARE @QRY VARCHAR(4000)
	DECLARE @MODELNO INT
	DECLARE @GROUPNO INT
	DECLARE @GROUPNOS VARCHAR(500)
	DECLARE MERGE_CURSOR CURSOR FOR
	SELECT /* UDP_MP_TBL_DUPCHECK_MODEL:05 */ MODELNO
		 , (SELECT TOP 1 GROUPNO FROM #DUPCHECK WHERE #DUPCHECK.MODELNO = #DUPCHECK_DUP.MODELNO ORDER BY GROUPNO) GROUPNO
		 , STUFF((SELECT ',' + CAST(GROUPNO AS VARCHAR(20)) FROM #DUPCHECK WHERE #DUPCHECK.MODELNO = #DUPCHECK_DUP.MODELNO FOR XML PATH ('')), 1, 1, '') GROUPNOS
	FROM #DUPCHECK_DUP
	
	OPEN MERGE_CURSOR
	
	FETCH NEXT FROM MERGE_CURSOR INTO @MODELNO, @GROUPNO, @GROUPNOS
	WHILE (@@FETCH_STATUS = 0)
	BEGIN
		SET @QRY = '
			DELETE /* UDP_MP_TBL_DUPCHECK_MODEL:06 */ FROM #DUPCHECK
			WHERE MODELNO = ' + CAST(@MODELNO AS VARCHAR(20)) + '
			  AND GROUPNO IN (' + @GROUPNOS + ')
			  AND GROUPNO <> ' + CAST(@GROUPNO AS VARCHAR(20)) + '
			
			UPDATE /* UDP_MP_TBL_DUPCHECK_MODEL:07 */ #DUPCHECK
			SET GROUPNO = ' + CAST(@GROUPNO AS VARCHAR(20)) + '
			WHERE GROUPNO IN (' + @GROUPNOS + ')
			  AND GROUPNO <> ' + CAST(@GROUPNO AS VARCHAR(20)) + '
		'
		EXEC (@QRY)
		
		FETCH NEXT FROM MERGE_CURSOR INTO @MODELNO, @GROUPNO, @GROUPNOS
	END
	
	CLOSE MERGE_CURSOR
	DEALLOCATE MERGE_CURSOR
	------------------------------------------------------------------------
	
	SELECT /* UDP_MP_TBL_DUPCHECK_MODEL:08 */ A.CA_CODE, A.JTYPE, A.INDEXER, A.INDEXER_TYPE, A.MODELNO, B.G_MODELNM, ISNULL(C.G_POPULAR, 0) G_POPULAR, DENSE_RANK() OVER(ORDER BY A.GROUPNO) GROUPNO, DENSE_RANK() OVER(PARTITION BY A.GROUPNO ORDER BY A.CONSTRAIN, CASE WHEN B.G_FACTORY <> '[불명]' AND B.G_BRAND <> '[불명]' THEN '0' ELSE '1' END, A.MODELNO) NRANK
	INTO #MERGE_DUPCHECK
	FROM #DUPCHECK A
		INNER JOIN GOODS B ON A.MODELNO = B.G_MODELNO
		LEFT JOIN GOODS_SUM C ON B.G_MODELNO = C.G_MODELNO
	WHERE B.G_MODELNM <> '모델삭제'
	OPTION (MAXDOP 1)
	
	DROP TABLE #DUPCHECK
	DROP TABLE #DUPCHECK_DUP
	CREATE /* UDP_MP_TBL_DUPCHECK_MODEL:09 */ NONCLUSTERED INDEX NCL_MERGE_DUPCHECK_T01 ON #MERGE_DUPCHECK(MODELNO ASC)
	CREATE /* UDP_MP_TBL_DUPCHECK_MODEL:10 */ NONCLUSTERED INDEX NCL_MERGE_DUPCHECK_T02 ON #MERGE_DUPCHECK(GROUPNO ASC)
	
	------------------------------------------------------------------------
	SELECT /* UDP_MP_TBL_DUPCHECK_MODEL:11 */ CA_CODE, JTYPE, INDEXER, INDEXER_TYPE, MODELNO, G_MODELNM, G_POPULAR, REFER_MODELNO
	FROM (
		SELECT CA_CODE, JTYPE, INDEXER, INDEXER_TYPE, MODELNO, G_MODELNM, G_POPULAR, GROUPNO
			 , STUFF((SELECT ',' + CAST(MODELNO AS VARCHAR(20)) FROM #MERGE_DUPCHECK B WHERE B.GROUPNO = A.GROUPNO AND NRANK <> 1 FOR XML PATH ('')), 1, 1, '') REFER_MODELNO
		FROM #MERGE_DUPCHECK A
		WHERE NRANK = 1
	) A
	WHERE REFER_MODELNO <> '' AND REFER_MODELNO IS NOT NULL
	ORDER BY CA_CODE, JTYPE, G_POPULAR DESC
	OPTION (MAXDOP 1)
END

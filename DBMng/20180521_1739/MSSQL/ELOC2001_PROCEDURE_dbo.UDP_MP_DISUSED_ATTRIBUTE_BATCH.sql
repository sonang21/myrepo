/* *************************************************************************
 * NAME : dbo.UDP_MP_DISUSED_ATTRIBUTE_BATCH
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2016-04-05 20:21:24.683
 *        Modify: 2018-05-03 17:23:34.803
 *        Backup: 20180521_1739
 ************************************************************************* */


CREATE PROC [DBO].[UDP_MP_DISUSED_ATTRIBUTE_BATCH]
	@CATEGORY VARCHAR(12),
	@MM_ID VARCHAR(20)
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	DECLARE @BATCH_FLAG SMALLINT

	SELECT @BATCH_FLAG = COUNT(*)
	FROM SEARCHKEY_193.ENURIDB.DBO.MM_ENURI
		INNER JOIN SEARCHKEY_193.ENURIDB.DBO.MM_CATEGORY ON MM_ID = MMC_ID
	WHERE MM_FLAG = '1' AND LEFT(MM_PARTCODE, 9) IN ('101202304', '101202305') AND MM_ID = @MM_ID AND (MMC_CATEGORY = LEFT(@CATEGORY, 2) OR MMC_CATEGORY = LEFT(@CATEGORY, 4))

	IF @BATCH_FLAG > 0
	BEGIN
		DECLARE @A_1 INT
		DECLARE @A_2 INT
		DECLARE @B_1 INT
		DECLARE @B_2 INT

		SELECT @A_1 = COUNT(*)
		FROM GOODS_ATTRIBUTE
		WHERE CATEGORY = @CATEGORY AND DEL_YN = 'N'

		SELECT @B_1 = COUNT(*)
		FROM GOODS_ATTRIBUTE A
			INNER JOIN GOODS_ATTRIBUTE_ELEMENT B ON A.ATTRIBUTE_ID = B.ATTRIBUTE_ID
		WHERE CATEGORY = @CATEGORY AND A.DEL_YN = 'N' AND B.DEL_YN = 'N'

		SELECT DISTINCT ATTRIBUTE_ID ID1, ATTRIBUTE_ELEMENT_ID ID2
		INTO #TEMP
		FROM GOODS_CATALOG_ATTRIBUTE
		WHERE DEL_YN = 'N' AND G_MODELNO IN (SELECT G_MODELNO FROM GOODS WHERE G_CATEGORY LIKE @CATEGORY + '%')

		UPDATE GOODS_ATTRIBUTE_ELEMENT
		SET DEL_YN = 'Y', UPD_DATE = GETDATE(), UPD_OPRT = @MM_ID
		OUTPUT @MM_ID, 0, DELETED.ATTRIBUTE_ID, DELETED.ATTRIBUTE_ELEMENT_ID, NULL, 'N', 'E', GETDATE()
			INTO GOODS_CATALOG_ATTRIBUTE_BACKUP
		FROM GOODS_ATTRIBUTE_ELEMENT A
			INNER JOIN GOODS_ATTRIBUTE B ON A.ATTRIBUTE_ID = B.ATTRIBUTE_ID
			LEFT JOIN #TEMP ON A.ATTRIBUTE_ID = ID1 AND A.ATTRIBUTE_ELEMENT_ID = ID2
		WHERE CATEGORY = LEFT(@CATEGORY, 4) AND ID1 IS NULL AND A.DEL_YN = 'N'

		UPDATE GOODS_ATTRIBUTE_ELEMENT
		SET DEL_YN = 'Y', UPD_DATE = GETDATE(), UPD_OPRT = @MM_ID
		OUTPUT @MM_ID, 0, DELETED.ATTRIBUTE_ID, DELETED.ATTRIBUTE_ELEMENT_ID, NULL, 'N', 'E', GETDATE()
			INTO GOODS_CATALOG_ATTRIBUTE_BACKUP
		FROM GOODS_ATTRIBUTE_ELEMENT A
			INNER JOIN GOODS_ATTRIBUTE B ON A.ATTRIBUTE_ID = B.ATTRIBUTE_ID
			LEFT JOIN #TEMP ON B.ATTRIBUTE_ID = ID1
		WHERE CATEGORY = LEFT(@CATEGORY, 4) AND ID1 IS NULL AND A.DEL_YN = 'N'

		UPDATE GOODS_ATTRIBUTE
		SET DEL_YN = 'Y', UPD_DATE = GETDATE(), UPD_OPRT = @MM_ID
		OUTPUT @MM_ID, 0, DELETED.ATTRIBUTE_ID, 0, NULL, 'N', 'A', GETDATE()
			INTO GOODS_CATALOG_ATTRIBUTE_BACKUP
		WHERE CATEGORY = LEFT(@CATEGORY, 4) AND DEL_YN = 'N' AND ATTRIBUTE_ID IN (
			SELECT ATTRIBUTE_ID
			FROM (
				SELECT A.ATTRIBUTE_ID, MIN(B.DEL_YN) DEL_MIN, MAX(B.DEL_YN) DEL_MAX
				FROM GOODS_ATTRIBUTE A
					INNER JOIN GOODS_ATTRIBUTE_ELEMENT B ON A.ATTRIBUTE_ID = B.ATTRIBUTE_ID
				WHERE CATEGORY = LEFT(@CATEGORY, 4)
				GROUP BY A.ATTRIBUTE_ID
			) A
			WHERE DEL_MIN = DEL_MAX AND DEL_MIN = 'Y')

		DELETE FROM GOODS_CATEGORY_ATTRIBUTE
		OUTPUT @MM_ID, 0, DELETED.ATTRIBUTE_ID, DELETED.DISPLAY_ORDER, DELETED.CATEGORY, 'N', 'C', GETDATE()
			INTO GOODS_CATALOG_ATTRIBUTE_BACKUP
		WHERE ATTRIBUTE_ID IN (SELECT ATTRIBUTE_ID FROM GOODS_ATTRIBUTE WHERE DEL_YN = 'Y' AND CATEGORY = LEFT(@CATEGORY, 4))

		SELECT @A_2 = COUNT(*)
		FROM GOODS_ATTRIBUTE
		WHERE CATEGORY = @CATEGORY AND DEL_YN = 'N'

		SELECT @B_2 = COUNT(*)
		FROM GOODS_ATTRIBUTE A
			INNER JOIN GOODS_ATTRIBUTE_ELEMENT B ON A.ATTRIBUTE_ID = B.ATTRIBUTE_ID
		WHERE CATEGORY = @CATEGORY AND A.DEL_YN = 'N' AND B.DEL_YN = 'N'

		DELETE FROM GOODS_CATALOG_ATTRIBUTE_BACKUP
        WHERE MM_ID = @MM_ID AND REGDATE < CONVERT(CHAR(10), GETDATE(), 23)

		SELECT '속성(1단계)	' + CAST(@A_1 AS VARCHAR(10)) + ' -> ' + CAST(@A_2 AS VARCHAR(10))
		UNION ALL 
		SELECT '속성원(2단계)	' + CAST(@B_1 AS VARCHAR(10)) + ' -> ' + CAST(@B_2 AS VARCHAR(10))
	END
END

/* *************************************************************************
 * NAME : dbo.UDP_PPS_CATALOG
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2017-11-02 10:39:09.267
 *        Modify: 2018-05-13 16:31:28.837
 *        Backup: 20180521_1739
 ************************************************************************* */


CREATE PROC [DBO].[UDP_PPS_CATALOG]
-- 조달청 카탈로그, 카탈로그 속성
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	-------------------------------------------------------------------
	-- 카탈로그
	DECLARE @QRY VARCHAR(4000)
	
	DROP TABLE TBL_PPS_CATALOG
	CREATE TABLE TBL_PPS_CATALOG(
		MODELNO		INT PRIMARY KEY,
		MODELNM		VARCHAR(140),
		CONDINAME	VARCHAR(50),
		CATEGORY	VARCHAR(12),
		CATEGORY_NM	VARCHAR(1000),
		FACTORY		VARCHAR(100),
		BRAND		VARCHAR(1000),
		IMGCHK3		CHAR(1),
		SPEC		VARCHAR(4000),
		POPULAR		INT,
		MINPRICE	INT,
		MINPRICE_DELIVERY CHAR(1)
	)
	
	DECLARE @CATEGORY VARCHAR(12)
	DECLARE FETCH_CURSOR CURSOR FOR
	SELECT DISTINCT CATEGORY
	FROM (
		SELECT CATEGORY + '000000' CATEGORY
		FROM TBL_PPS_CATEGORY A
		WHERE A.LEVEL = 1
		  AND NOT EXISTS(SELECT TOP 1 1 FROM TBL_PPS_CATEGORY B WHERE B.CATEGORY LIKE A.CATEGORY + '%' AND B.LEVEL > 1)
		UNION ALL
		SELECT CATEGORY + '0000' CATEGORY
		FROM TBL_PPS_CATEGORY A
		WHERE A.LEVEL = 2
		  AND NOT EXISTS(SELECT TOP 1 1 FROM TBL_PPS_CATEGORY B WHERE B.CATEGORY LIKE A.CATEGORY + '%' AND B.LEVEL > 2)
		UNION ALL
		SELECT CATEGORY + '00' CATEGORY
		FROM TBL_PPS_CATEGORY A
		WHERE A.LEVEL = 3
		  AND NOT EXISTS(SELECT TOP 1 1 FROM TBL_PPS_CATEGORY B WHERE B.CATEGORY LIKE A.CATEGORY + '%' AND B.LEVEL > 3)
		UNION ALL
		SELECT CATEGORY CATEGORY
		FROM TBL_PPS_CATEGORY
		WHERE LEVEL = 4
	) A
	ORDER BY CATEGORY
	
	OPEN FETCH_CURSOR
	
	FETCH NEXT FROM FETCH_CURSOR INTO @CATEGORY
	WHILE @@FETCH_STATUS = 0
	BEGIN
		SET @QRY = '
			INSERT INTO TBL_PPS_CATALOG(MODELNO,MODELNM,CONDINAME,CATEGORY,FACTORY,BRAND,IMGCHK3,SPEC,POPULAR,MINPRICE,MINPRICE_DELIVERY)
			SELECT MODELNO,MODELNM,CONDINAME,CA_CODE,FACTORY,BRAND,IMGCHK3,SPEC,POPULAR,MINPRICE,MINPRICE_DELIVERY
			FROM OPENQUERY(ORADB, ''SELECT A.MODELNO, NVL(A.MODELNM, '''' '''') MODELNM, NVL(A.CONDINAME, '''' '''') CONDINAME, A.CA_CODE, NVL(A.FACTORY, '''' '''') FACTORY, NVL(A.BRAND, '''' '''') BRAND, A.IMGCHK3, NVL(A.SPEC, '''' '''') SPEC, NVL(B.POPULAR, 0) POPULAR, NVL(B.MINPRICE, 0) MINPRICE, CASE WHEN EXISTS(SELECT C.PL_NO FROM TBL_PRICELIST C WHERE A.MODELNO = C.MODELNO AND C.PRICE = B.MINPRICE AND C.SRVFLAG IN (''''0'''',''''R'''',''''L'''',''''B'''') AND C.STATUS NOT IN (''''3'''',''''4'''',''''5'''') AND NVL(C.OPTION_FLAG2, ''''0'''') = ''''0'''' AND C.DELIVERYTYPE2 = ''''1'''' AND C.DELIVERYINFO2 = 0 AND ROWNUM = 1) THEN ''''Y'''' ELSE ''''N'''' END MINPRICE_DELIVERY
									FROM TBL_GOODS A
										INNER JOIN TBL_GOODS_SUM B ON A.MODELNO = B.MODELNO
									WHERE A.CA_CODE = ''''' + @CATEGORY + ''''' AND A.CONSTRAIN = ''''1'''' AND A.JOBCODE > ''''0'''' AND B.MALLCNT > 0'')
			OPTION (MAXDOP 1)'
		EXEC (@QRY)
		
		FETCH NEXT FROM FETCH_CURSOR INTO @CATEGORY
	END
	
	CLOSE FETCH_CURSOR
	DEALLOCATE FETCH_CURSOR
	-------------------------------------------------------------------
	
	-------------------------------------------------------------------
	-- 카탈로그 속성
	DROP TABLE TBL_PPS_CATALOG_ATTRIBUTE
	CREATE TABLE TBL_PPS_CATALOG_ATTRIBUTE(
		G_MODELNO		INT NOT NULL,
		CATEGORY		VARCHAR(12),
		ATTRIBUTE_ID	INT NOT NULL,
		ATTRIBUTE_ELEMENT_ID	INT NOT NULL,
		ATTRIBUTE_VALUE	VARCHAR(500),
		INS_DATE		DATETIME,
		UPD_DATE		DATETIME
	)
	ALTER TABLE TBL_PPS_CATALOG_ATTRIBUTE ADD CONSTRAINT [CL_TBL_PPS_CATALOG_ATTRIBUTE] PRIMARY KEY CLUSTERED(G_MODELNO ASC, ATTRIBUTE_ID ASC, ATTRIBUTE_ELEMENT_ID ASC)
	CREATE NONCLUSTERED INDEX [NCL_TBL_PPS_CATALOG_ATTRIBUTE] ON TBL_PPS_CATALOG_ATTRIBUTE(ATTRIBUTE_ID ASC, ATTRIBUTE_ELEMENT_ID ASC)
	
	INSERT INTO TBL_PPS_CATALOG_ATTRIBUTE(G_MODELNO, CATEGORY, ATTRIBUTE_ID, ATTRIBUTE_ELEMENT_ID, ATTRIBUTE_VALUE, INS_DATE, UPD_DATE)
	SELECT A.G_MODELNO, B.CATEGORY, A.ATTRIBUTE_ID, A.ATTRIBUTE_ELEMENT_ID, ISNULL((CASE WHEN USE_CLASS_CODE = '2' THEN ATTRIBUTE_VALUE ELSE ATTRIBUTE_ELEMENT END), ''), A.INS_DATE, A.UPD_DATE
	FROM GOODS_CATALOG_ATTRIBUTE A
		INNER JOIN TBL_PPS_CATALOG B ON MODELNO = G_MODELNO
		INNER JOIN TBL_PPS_ATTRIBUTE C ON C.ATTRIBUTE_ID = A.ATTRIBUTE_ID
		INNER JOIN TBL_PPS_ATTRIBUTE_ELEMENT D ON D.ATTRIBUTE_ID = A.ATTRIBUTE_ID AND D.ATTRIBUTE_ELEMENT_ID = A.ATTRIBUTE_ELEMENT_ID
	WHERE A.DEL_YN = 'N'
	OPTION (MAXDOP 1)
	-------------------------------------------------------------------
	
	-- 데이터 보정
	DELETE FROM TBL_PPS_CATALOG
	WHERE MODELNO NOT IN (SELECT DISTINCT G_MODELNO FROM TBL_PPS_CATALOG_ATTRIBUTE)
	OPTION (MAXDOP 1)
	
	UPDATE TBL_PPS_CATALOG
	SET CATEGORY_NM = 
		ISNULL((SELECT TOP 1 CATEGORY_NM FROM TBL_PPS_CATEGORY WHERE LEVEL = 1 AND TBL_PPS_CATEGORY.CATEGORY = SUBSTRING(TBL_PPS_CATALOG.CATEGORY, 1, 2)), '')
	  + '@' + ISNULL((SELECT TOP 1 CATEGORY_NM FROM TBL_PPS_CATEGORY WHERE LEVEL = 2 AND TBL_PPS_CATEGORY.CATEGORY = SUBSTRING(TBL_PPS_CATALOG.CATEGORY, 1, 4)), '')
	  + '@' + ISNULL((SELECT TOP 1 CATEGORY_NM FROM TBL_PPS_CATEGORY WHERE LEVEL = 3 AND TBL_PPS_CATEGORY.CATEGORY = SUBSTRING(TBL_PPS_CATALOG.CATEGORY, 1, 6)), '')
	  + '@' + ISNULL((SELECT TOP 1 CATEGORY_NM FROM TBL_PPS_CATEGORY WHERE LEVEL = 4 AND TBL_PPS_CATEGORY.CATEGORY = SUBSTRING(TBL_PPS_CATALOG.CATEGORY, 1, 8)), '')
	OPTION (MAXDOP 1)
	
	SELECT MODELNO, MODELNM, CONDINAME, CATEGORY, CATEGORY_NM, FACTORY, BRAND, IMGCHK3, SPEC, POPULAR, MINPRICE, MINPRICE_DELIVERY
	FROM TBL_PPS_CATALOG
	ORDER BY CATEGORY, POPULAR DESC
	/*
	SELECT G_MODELNO, CATEGORY, ATTRIBUTE_ID, ATTRIBUTE_ELEMENT_ID, ATTRIBUTE_VALUE, INS_DATE, UPD_DATE
	FROM TBL_PPS_CATALOG_ATTRIBUTE
	WHERE ATTRIBUTE_VALUE <> ''
	ORDER BY CATEGORY, G_MODELNO, ATTRIBUTE_ID, ATTRIBUTE_ELEMENT_ID
	*/
END

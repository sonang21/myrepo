/* *************************************************************************
 * NAME : dbo.UDP_SHOP_EXCEPTION_PROC
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2006-04-06 14:41:39.083
 *        Modify: 2018-05-03 17:23:34.173
 *        Backup: 20180521_1739
 ************************************************************************* */

CREATE PROC UDP_SHOP_EXCEPTION_PROC
	@SHOP_CODE INT
AS
BEGIN
	SET NOCOUNT ON
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED


	--1. 해당 모델에 등록된 상품이 3이하인넘들의 상태확인후 '9'-->'0'변경
	BEGIN TRAN
	UPDATE PRICELIST 
	SET PL_SRVFLAG='0', PL_STATUS='1'
	WHERE	
		PL_VCODE = @SHOP_CODE
		AND PL_MODELNO > 0
		AND PL_STATUS < '3'
		AND PL_SRVFLAG = '9'
		AND PL_MODELNO IN (
				SELECT	
					PL_MODELNO
				FROM PRICELIST WITH (READUNCOMMITTED) 	
				WHERE	
					PL_VCODE = @SHOP_CODE
					AND PL_MODELNO > 0
					AND PL_STATUS < '3'
					AND (PL_SRVFLAG = '0' OR PL_SRVFLAG = '9')
				GROUP BY	
					PL_MODELNO
				HAVING COUNT(*) <= 3
				)
	IF(@@ERROR <> 0)
	BEGIN
		ROLLBACK TRANSACTION
		RAISERROR ('1. 특정업체 예외처리중 에러가 발생했습니다.', 5, 1) WITH LOG
		Return -1
	END
	COMMIT TRAN

	--2. 해당 모델에 등록된 상품이 3이상인넘들의 상태확인후 -->'9'변경	
	DECLARE @TMP TABLE(
	IDX INT IDENTITY(1,1),
	MODELNO INT 
	)

	INSERT INTO @TMP (MODELNO)
	SELECT	
		PL_MODELNO
	FROM PRICELIST WITH (READUNCOMMITTED) 	
	WHERE	
		PL_VCODE = @SHOP_CODE
		AND PL_MODELNO > 0
		AND PL_STATUS < '3'
		AND (PL_SRVFLAG = '0' OR PL_SRVFLAG = '9')
	GROUP BY	
		PL_MODELNO
	HAVING COUNT(*) > 3

	DECLARE @MAX INT
	DECLARE @MODELNO INT 
	SELECT @MAX= MAX(IDX) FROM @TMP
	
	WHILE @MAX<>0
	BEGIN
		SELECT @MODELNO=MODELNO FROM @TMP WHERE IDX=@MAX

		BEGIN TRAN
		UPDATE PRICELIST
		SET PL_SRVFLAG=(CASE WHEN PL_SRVFLAG='9' THEN '0' ELSE PL_SRVFLAG END)
			, PL_STATUS='1'
		WHERE PL_NO IN (
			SELECT TOP 3 PL_NO
			FROM PRICELIST (NOLOCK)
			WHERE PL_VCODE = @SHOP_CODE
				AND PL_MODELNO=@MODELNO
				AND PL_STATUS < '3'
				AND (PL_SRVFLAG = '0' OR PL_SRVFLAG = '9')
			ORDER BY PL_PRICE ASC)
		IF(@@ERROR <> 0)
		BEGIN
			ROLLBACK TRANSACTION
			RAISERROR ('2. 특정업체 예외처리중 에러가 발생했습니다.', 5, 1) WITH LOG
			Return -1
		END
		COMMIT TRAN

		BEGIN TRAN
		UPDATE PRICELIST
		SET PL_SRVFLAG='9', PL_STATUS='1'
		WHERE PL_VCODE = @SHOP_CODE
			AND PL_MODELNO=@MODELNO
			AND PL_STATUS < '3'
			AND (PL_SRVFLAG = '0' OR PL_SRVFLAG = '9')
			AND PL_NO NOT IN(
					SELECT TOP 3 PL_NO
					FROM PRICELIST (NOLOCK)
					WHERE PL_VCODE = @SHOP_CODE
						AND PL_MODELNO=@MODELNO
						AND PL_STATUS < '3'
						AND (PL_SRVFLAG = '0' OR PL_SRVFLAG = '9')
					ORDER BY PL_PRICE ASC
					)
		IF(@@ERROR <> 0)
		BEGIN
			ROLLBACK TRANSACTION
			RAISERROR ('3. 특정업체 예외처리중 에러가 발생했습니다.', 5, 1) WITH LOG
			Return -1
		END
		COMMIT TRAN

		SET @MAX = @MAX-1
	END
END

/* *************************************************************************
 * NAME : dbo.UDP_MP_GOODS_CATALOG_ATTRIBUTE_UPLOAD
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2016-12-16 09:47:46.16
 *        Modify: 2018-05-03 17:23:35.567
 *        Backup: 20180521_1739
 ************************************************************************* */


CREATE PROC [DBO].[UDP_MP_GOODS_CATALOG_ATTRIBUTE_UPLOAD]
	@MODELNO INT
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	DECLARE @CATEGORY VARCHAR(12)
	SELECT @CATEGORY = G_CATEGORY
	FROM GOODS
	WHERE G_MODELNO = @MODELNO
	

	SET TRANSACTION ISOLATION LEVEL READ COMMITTED;
	--##################################################################
	DECLARE @QRY VARCHAR(8000)
	SET @QRY = 'DELETE OPENQUERY(ORADB, ''SELECT MODELNO, ATTRIBUTE_ID, ATTRIBUTE_ELEMENT_ID, ATTRIBUTE_VALUE, DISPLAY_ORDER, TAG_ORDER, REGDATE FROM TBL_GOODS_ATTRIBUTE_CATALOG WHERE MODELNO = ' + CAST(@MODELNO AS VARCHAR(20)) + ''')'
	EXEC (@QRY)
	
	INSERT OPENQUERY(ORADB, 'SELECT MODELNO, ATTRIBUTE_ID, ATTRIBUTE_ELEMENT_ID, ATTRIBUTE_VALUE, DISPLAY_ORDER, TAG_ORDER, REGDATE FROM TBL_GOODS_ATTRIBUTE_CATALOG')
	SELECT G_MODELNO, A.ATTRIBUTE_ID, A.ATTRIBUTE_ELEMENT_ID, ATTRIBUTE_VALUE, D.DISPLAY_ORDER, TAG_ORDER, GETDATE()
	FROM GOODS_CATALOG_ATTRIBUTE A
		INNER JOIN GOODS_ATTRIBUTE B ON A.ATTRIBUTE_ID = B.ATTRIBUTE_ID
		INNER JOIN GOODS_ATTRIBUTE_ELEMENT C ON A.ATTRIBUTE_ID = C.ATTRIBUTE_ID AND A.ATTRIBUTE_ELEMENT_ID = C.ATTRIBUTE_ELEMENT_ID
		INNER JOIN GOODS_CATEGORY_ATTRIBUTE D ON A.ATTRIBUTE_ID = D.ATTRIBUTE_ID AND D.CATEGORY IN (SELECT TOP 1 CATEGORY FROM GOODS_CATEGORY_ATTRIBUTE WHERE CATEGORY = LEFT(@CATEGORY, 4) OR CATEGORY = LEFT(@CATEGORY, 6) OR CATEGORY = LEFT(@CATEGORY, 8) ORDER BY LEN(CATEGORY) DESC)
	WHERE A.DEL_YN = 'N'
		AND B.DEL_YN = 'N'
		AND C.DEL_YN = 'N'
		AND G_MODELNO = @MODELNO
		AND LTRIM(RTRIM(ISNULL(CASE WHEN USE_CLASS_CODE <> '2' THEN ATTRIBUTE_ELEMENT ELSE ATTRIBUTE_VALUE END, ''))) <> ''
	--##################################################################
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	EXEC UDP_MP_GOODS_CATALOG_ATTRIBUTE_SPEC_MOBILE @MODELNO
	EXEC UDP_MP_GOODS_SPECNO_ATTRIBUTE @MODELNO
END

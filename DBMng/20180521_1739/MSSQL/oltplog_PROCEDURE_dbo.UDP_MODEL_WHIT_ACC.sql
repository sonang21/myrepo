/* *************************************************************************
 * NAME : dbo.UDP_MODEL_WHIT_ACC
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2018-05-14 13:51:25.57
 *        Modify: 2018-05-14 13:51:25.57
 *        Backup: 20180521_1739
 ************************************************************************* */

CREATE PROC UDP_MODEL_WHIT_ACC
	@DTE_IN SMALLDATETIME=''
AS
BEGIN
	SET NOCOUNT ON
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
	/*
	에누리 상품창 로그처리
	- 주별, 월별 상품창 클릭수 통계처리.
	- 중복무시, 중복제거 후 처리.
	- 임시ID & IP 기준
	        (두 가지 중 한가지만 동일해도 중복처리.
	        단, 프록시서버를 사용하는 대기업 ID는 중복허용 하도록 처리) TBL_MODEL_LOG_FILTERIP		
	*/
	--DECLARE @DTE_IN SMALLDATETIME
	--SET @DTE_IN='20070324'
	DECLARE @DTE_DAY CHAR(8)
	DECLARE @WEEK_STAT TABLE
	(
		IDX INT IDENTITY(1,1),
		MODELNO INT,
		CATEGORY CHAR(8),
		WK_HIT INT,
		WK_DUP_HIT INT
	)	
	


	--SET @DTE_IN='20070301'

	IF @DTE_IN = '' OR @DTE_IN IS NULL
	BEGIN
		SET @DTE_DAY = CONVERT(CHAR(8),  DATEADD(D, -1, GETDATE()),112)
	END
	ELSE
	BEGIN
		SET @DTE_DAY =  CONVERT(CHAR(8), @DTE_IN, 112)
	END 
	PRINT '@DTE_DAY=' + @DTE_DAY
	--@DTE_DAY사용
	
	

	DECLARE @WEEK SMALLINT
	SET @WEEK = DATEDIFF(WK, '20050102', @DTE_DAY)
	PRINT '@WEEK=' + CAST(@WEEK AS VARCHAR(12))

	DECLARE @DTE_STARTDAY CHAR(8)
	DECLARE @DTE_ENDDAY CHAR(8)
	SET @DTE_STARTDAY = CONVERT(CHAR(8), DBO.UDF_START_WEEK(@DTE_DAY), 112)
	SET @DTE_ENDDAY = CONVERT(CHAR(8), DBO.UDF_START_WEEK(@DTE_DAY)+7,112)
	PRINT '@DTE_STARTDAY=' + @DTE_STARTDAY
	PRINT '@DTE_ENDDAY=' + @DTE_ENDDAY
	--주간 히트수(중복무시)
	INSERT INTO @WEEK_STAT
	(MODELNO, CATEGORY, WK_HIT, WK_DUP_HIT)

	SELECT  MODELNO, 
		ISNULL(G_CATEGORY,'00000000') AS G_CATEGORY, 
		CNT, 0
	FROM (
		SELECT  MODELNO, COUNT(*) AS CNT
		FROM TBL_MODEL_LOG
		WHERE REGDATE >= @DTE_STARTDAY 
			AND REGDATE < @DTE_ENDDAY
		GROUP BY  MODELNO
		) A
	INNER JOIN ANALSTORE.DBO.GOODS
	ON MODELNO=G_MODELNO

	
	--주간히트수(중복제거)
	INSERT INTO @WEEK_STAT
	(MODELNO, CATEGORY, WK_HIT, WK_DUP_HIT)

	SELECT 	MODELNO,
		ISNULL(G_CATEGORY,'00000000') AS G_CATEGORY, 
		0, CNT
	FROM (
		SELECT MODELNO, SUM(CNT) AS CNT
		FROM (
			SELECT REGDATE, MODELNO, 1 AS CNT
			FROM (
				SELECT  REGDATE, MODELNO, MAX(TMPID) AS TMPID
				FROM TBL_MODEL_LOG
				WHERE REGDATE >= @DTE_STARTDAY 
					AND REGDATE < @DTE_ENDDAY
					AND USERIP NOT IN(SELECT USERIP FROM TBL_MODEL_LOG_FILTERIP)
				GROUP BY  REGDATE, MODELNO, USERIP
			) A
			GROUP BY REGDATE, MODELNO, TMPID
			UNION ALL	
			SELECT REGDATE, MODELNO,1 AS CNT
			FROM TBL_MODEL_LOG
			WHERE REGDATE >= @DTE_STARTDAY 
				AND REGDATE < @DTE_ENDDAY
				AND USERIP IN(SELECT USERIP FROM TBL_MODEL_LOG_FILTERIP)
			GROUP BY REGDATE, MODELNO, TMPID
		)B
		GROUP BY MODELNO
	) C
	INNER JOIN ANALSTORE.DBO.GOODS
	ON MODELNO=G_MODELNO
	
--SELECT MODELNO, @WEEK AS MW_WK, MAX(CATEGORY), SUM(WK_HIT) AS MW_HIT, SUM(WK_DUP_HIT) AS MW_DUP_HIT
--FROM @WEEK_STAT
--GROUP BY MODELNO		
	
	BEGIN TRAN
		DELETE TBL_MODEL_WHIT WHERE MW_WK=@WEEK

		INSERT INTO TBL_MODEL_WHIT 
		(G_MODELNO, MW_WK, G_CATEGORY, MW_HIT, MW_DUP_HIT )	
		SELECT MODELNO, @WEEK AS MW_WK, MAX(CATEGORY), SUM(WK_HIT) AS MW_HIT, SUM(WK_DUP_HIT) AS MW_DUP_HIT
		FROM @WEEK_STAT
		GROUP BY MODELNO
	
	COMMIT TRAN


END






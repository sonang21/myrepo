/* *************************************************************************
 * NAME : dbo.UDP_MP_GOODS_ATTRIBUTE_TARGET_INS
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2016-04-05 20:27:11.44
 *        Modify: 2018-05-03 17:23:35.447
 *        Backup: 20180521_1737
 ************************************************************************* */


CREATE PROC [DBO].[UDP_MP_GOODS_ATTRIBUTE_TARGET_INS]
	@CATE AS VARCHAR(8)
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	TRUNCATE TABLE GOODS_ATTRIBUTE_TARGET;
	
	DECLARE @I INT
		
	SELECT G_MODELNO, G_CATEGORY, LTRIM(RTRIM(DBO.UDF_RTN_REPLACE_STRING(G_MODELNM))) G_MODELNM, LTRIM(RTRIM(ISNULL(G_BRAND, ''))) G_BRAND
	INTO #TEMP_GOODS
	FROM GOODS
	WHERE G_CATEGORY LIKE @CATE + '%'
	OPTION (MAXDOP 1)
	
	SELECT @I = COUNT(*)
	FROM #TEMP_GOODS
	
	WHILE @I > 0
	BEGIN
		INSERT INTO GOODS_ATTRIBUTE_TARGET(G_MODELNO, G_CATEGORY, G_MODELNM, G_BRAND)
		SELECT G_MODELNO, G_CATEGORY, LTRIM(RTRIM(CASE WHEN CHARINDEX(' ', G_MODELNM) > 0 THEN SUBSTRING(G_MODELNM, 1, CHARINDEX(' ', G_MODELNM)) ELSE G_MODELNM END)), G_BRAND
		FROM #TEMP_GOODS
		
		UPDATE #TEMP_GOODS
		SET G_MODELNM = LTRIM(RTRIM(CASE WHEN CHARINDEX(' ', G_MODELNM) > 0 THEN SUBSTRING(G_MODELNM, CHARINDEX(' ', G_MODELNM), 1000) ELSE '' END))
		
		DELETE FROM #TEMP_GOODS
		WHERE G_MODELNM = ''
		
		SELECT @I = COUNT(*)
		FROM #TEMP_GOODS
	END
END

/* *************************************************************************
 * NAME : dbo.USP_TBL_AUTO_MATCHING_PRICE_BATCH
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2014-07-31 10:53:47.623
 *        Modify: 2018-05-16 11:00:33.42
 *        Backup: 20180521_1737
 ************************************************************************* */


CREATE PROC [DBO].[USP_TBL_AUTO_MATCHING_PRICE_BATCH]
	@CATEGORY VARCHAR(12)
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	SELECT A.PL_NO PL_NO
	     , PL_NO2 PL_NO_GROUP
		 , G_MODELNO MODELNO
		 , G_CATEGORY CATEGORY
	INTO #MATCHING_TABLE
	FROM PRICELIST A
		INNER JOIN TBL_AUTO_MATCHING_PRICE B ON A.PL_NO = B.PL_NO
		INNER JOIN (
			SELECT PL_NO_GROUP
			FROM (
				SELECT PL_NO2 PL_NO_GROUP
				     , MIN(JOB_FLAG) JOB_FLAG
					 , MIN(PRICE) PRICE
					 , MIN(MINPRICE) MINPRICE
				FROM TBL_AUTO_MATCHING_PRICE
				WHERE MODEL_CA_CODE LIKE @CATEGORY + '%' --AND PL_NO2 IN (872374199, 929174367)
				GROUP BY PL_NO2
			) A
			WHERE JOB_FLAG = '0' AND PRICE > MINPRICE
		) C ON PL_NO2 = PL_NO_GROUP AND JOB_FLAG = '0' AND MODEL_CA_CODE LIKE @CATEGORY + '%'
		INNER JOIN GOODS ON MODELNO = G_MODELNO
	WHERE PL_STATUS < '3' AND PL_SRVFLAG = '0' AND PL_MODELNO = 0
	
	-- JOB_FLAG 수정
	UPDATE TBL_AUTO_MATCHING_PRICE
	SET JOB_FLAG = '6'
	WHERE MODEL_CA_CODE LIKE @CATEGORY + '%' AND PL_NO IN (SELECT PL_NO FROM #MATCHING_TABLE)
	
	-- PRICELIST 매칭
	CREATE TABLE #UPDATE_TARGET(PL_NO BIGINT, PL_MODELNO INT)
	UPDATE PRICELIST
	SET PL_MODELNO = MODELNO, PL_CATEGORY = CATEGORY, PL_FINALUSEDFLAG = 1
	OUTPUT DELETED.PL_NO, INSERTED.PL_MODELNO INTO #UPDATE_TARGET
	FROM PRICELIST A
		INNER JOIN #MATCHING_TABLE B ON A.PL_NO = B.PL_NO
	OPTION (MAXDOP 1)
	-- 분산큐(PRICELIST)
	INSERT INTO TB_PL_CHG_HST(PL_NO, MODEL_NO, PGM_ID, DAT_CHG_DCD, FRW_YN, ORGN_DTM)
	SELECT PL_NO, PL_MODELNO, 'db_proc', 'U', 'N', GETDATE()
	FROM #UPDATE_TARGET
	
	-- 로그
	INSERT INTO LOGDB.DBO.JOB_PRICELIST (JP_PLISTNO, JP_MODELNO, JP_VCODE, JP_ID, JP_FLAG, JP_DATE, JP_GOODSNM, JP_PRICE, JP_CATEGORY, JP_PRICE_MOBILE, JP_NOTE)
	SELECT PL_NO, PL_MODELNO, PL_VCODE, 'MP_SGM_AUTO', '0', GETDATE(), PL_GOODSNM, PL_PRICE, PL_CATEGORY, PL_INSTANCE_PRICE, PL_NOTE
    FROM PRICELIST
    WHERE PL_NO IN (SELECT PL_NO FROM #UPDATE_TARGET)
	OPTION (MAXDOP 1)
	
	-- SGM 로그
	DELETE FROM TBL_AUTO_MATCHING_PRICE_LOG
	WHERE PL_NO IN (
		SELECT DISTINCT PL_NO
		FROM #MATCHING_TABLE
		UNION ALL
		SELECT DISTINCT PL_NO_GROUP
		FROM #MATCHING_TABLE
	)
	
	INSERT INTO TBL_AUTO_MATCHING_PRICE_LOG(PL_NO, PL_NO_GROUP, FLAG, REGDATE)
	SELECT PL_NO, PL_NO_GROUP, FLAG, REGDATE
	FROM (
		SELECT DISTINCT PL_NO, PL_NO_GROUP, '0' FLAG, GETDATE() REGDATE
		FROM #MATCHING_TABLE
		UNION ALL
		SELECT DISTINCT PL_NO_GROUP, PL_NO_GROUP, '0' FLAG, GETDATE() REGDATE
		FROM #MATCHING_TABLE
	) A
END

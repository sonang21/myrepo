/* *************************************************************************
 * NAME : dbo.UDP_MP_GOODS_CATALOG_ATTRIBUTE_SPEC_BATCH
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2016-08-02 17:04:34.213
 *        Modify: 2018-05-03 17:23:35.807
 *        Backup: 20180521_1737
 ************************************************************************* */


CREATE PROC [DBO].[UDP_MP_GOODS_CATALOG_ATTRIBUTE_SPEC_BATCH]
	@CATEGORY VARCHAR(12)
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	DECLARE @MODELNO INT
	DECLARE @SZLPTAG1 VARCHAR(8000)
	DECLARE @SZLPTAG2 VARCHAR(8000)
	DECLARE @SZVIPTAG VARCHAR(8000)
	DECLARE @QRY VARCHAR(8000)

	SELECT G_MODELNO
	INTO #TARGET_GOODS
	FROM GOODS
	WHERE G_CATEGORY LIKE @CATEGORY + '%'
	OPTION (MAXDOP 1)
	
	DELETE FROM LOGDB.DBO.ATTRIBUTE_SPEC_BATCH_LOG WHERE CA_CODE = @CATEGORY
	INSERT INTO LOGDB.DBO.ATTRIBUTE_SPEC_BATCH_LOG(CA_CODE, CNT) VALUES(@CATEGORY, (SELECT COUNT(*) FROM #TARGET_GOODS))

	DECLARE CURSOR_FETCH CURSOR FOR
	SELECT G_MODELNO
	FROM #TARGET_GOODS
	ORDER BY G_MODELNO

	OPEN CURSOR_FETCH

	FETCH NEXT FROM CURSOR_FETCH INTO @MODELNO
	WHILE @@FETCH_STATUS = 0
	BEGIN
		EXEC UDP_MP_GOODS_CATALOG_ATTRIBUTE_SPEC_MOBILE @MODELNO
		EXEC UDP_MP_GOODS_SPECNO_ATTRIBUTE @MODELNO

		SET @SZLPTAG1 = ''
		SET @SZLPTAG2 = ''
		SET @SZVIPTAG = ''
		EXEC UDP_MP_GOODS_CATALOG_ATTRIBUTE_SPEC_MAKE @MODELNO, '', 'Y', 'N', @SZLPTAG1 OUTPUT, @SZLPTAG2 OUTPUT, @SZVIPTAG OUTPUT

		SET TRANSACTION ISOLATION LEVEL READ COMMITTED;
		--##################################################################
		SET @QRY = 'DECLARE @CNT INT; ' + 
		           'SELECT @CNT = COUNT(*) FROM OPENQUERY(ORADB, ''SELECT * FROM TBL_GOODS_CHANGE WHERE MODELNO = ' + CAST(@MODELNO AS VARCHAR(20)) + '''); ' + 
				   'IF @CNT > 0 ' + 
				   'BEGIN ' + 
						'UPDATE OPENQUERY(ORADB, ''SELECT SPEC_LP_TAG, SPEC_VIP_TAG, SPEC_LP_TAG2 FROM TBL_GOODS_CHANGE WHERE MODELNO = ' + CAST(@MODELNO AS VARCHAR(20)) + ''') ' + 
						'SET SPEC_LP_TAG = ''' + REPLACE(@SZLPTAG1, '''', '''''')  + ''', SPEC_VIP_TAG = ''' + REPLACE(@SZVIPTAG, '''', '''''')  + ''', SPEC_LP_TAG2 = ''' + REPLACE(@SZLPTAG2, '''', '''''') + '''; ' + 
				   'END ' + 
				   'ELSE IF ''' + REPLACE(@SZLPTAG1, '''', '''''') + ''' <> '''' OR ''' + REPLACE(@SZVIPTAG, '''', '''''') + ''' <> '''' OR ''' + REPLACE(@SZLPTAG2, '''', '''''') + ''' <> ''''' +
				   'BEGIN ' + 
						'INSERT INTO OPENQUERY(ORADB, ''SELECT MODELNO, SPEC_LP_TAG, SPEC_VIP_TAG, SPEC_LP_TAG2 FROM TBL_GOODS_CHANGE'') ' + 
						'VALUES(' + CAST(@MODELNO AS VARCHAR(20)) + ', ''' + REPLACE(@SZLPTAG1, '''', '''''') + ''', ''' + REPLACE(@SZVIPTAG, '''', '''''') + ''', ''' + REPLACE(@SZLPTAG2, '''', '''''') + ''') ' + 
				   'END '
		EXEC (@QRY)
		--##################################################################
		SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

		FETCH NEXT FROM CURSOR_FETCH INTO @MODELNO
	END

	CLOSE CURSOR_FETCH
	DEALLOCATE CURSOR_FETCH

	UPDATE LOGDB.DBO.ATTRIBUTE_SPEC_BATCH_LOG
	SET END_DATE = GETDATE()
	WHERE CA_CODE = @CATEGORY
END

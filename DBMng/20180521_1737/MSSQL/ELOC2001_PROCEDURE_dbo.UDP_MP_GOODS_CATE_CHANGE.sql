/* *************************************************************************
 * NAME : dbo.UDP_MP_GOODS_CATE_CHANGE
 * TYPE : PROCEDURE (SQL_STORED_PROCEDURE)
 * TIME : Create: 2016-04-05 20:25:36.57
 *        Modify: 2018-05-03 17:23:35.257
 *        Backup: 20180521_1737
 ************************************************************************* */


CREATE PROCEDURE [DBO].[UDP_MP_GOODS_CATE_CHANGE]
	@PRE_CATE VARCHAR(12),
	@NEW_CATE VARCHAR(12),
	@MM_ID VARCHAR(12),
	@MM_NM VARCHAR(12)
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	
	DECLARE @AFF_ROWS INT
	SET @AFF_ROWS = 0
	
	-- 다른 소분류로 변경할 때, 다르면 그룹툴 상품 초기화
	IF LEFT(@PRE_CATE, 6) <> LEFT(@NEW_CATE, 6)
		BEGIN
			SELECT G_MODELNO, G_MODELNO_GROUP
			INTO #TEMP
			FROM GOODS
			WHERE G_CATEGORY = @PRE_CATE
			
			--### 그룹툴 조건명 속성처리
			SELECT ATTRIBUTE_ID
			INTO #TEMP_ATTRIBUTE_GROUP
			FROM GOODS_ATTRIBUTE
			WHERE DEL_YN = 'N' AND CATEGORY = '' AND CHARINDEX('조건명_', MANAGE_ATTRIBUTE_NM) > 0
			
			SELECT DISTINCT G_MODELNO
			INTO #TEMP_ATTRIBUTE
			FROM GOODS
			WHERE G_MODELNO IN (SELECT G_MODELNO FROM #TEMP) OR G_MODELNO_GROUP IN (SELECT G_MODELNO_GROUP FROM #TEMP WHERE G_MODELNO = G_MODELNO_GROUP)
			OPTION (MAXDOP 2)
			
			UPDATE GOODS_CATALOG_ATTRIBUTE
			SET DEL_YN = 'Y', UPD_DATE = GETDATE(), UPD_OPRT = 'MP_batch'
			WHERE DEL_YN = 'N' AND ATTRIBUTE_ID IN (SELECT ATTRIBUTE_ID FROM #TEMP_ATTRIBUTE_GROUP)
			  AND G_MODELNO IN (SELECT G_MODELNO FROM #TEMP_ATTRIBUTE)
			
			UPDATE GOODS
			SET G_FLAG = '1', G_MODELNO_GROUP = NULL, G_CONDITION_NUMBER = NULL, G_GROUP_FLAG = NULL, G_GROUP_ATTRIBUTE_ID = NULL
			WHERE G_MODELNO_GROUP IN (SELECT G_MODELNO_GROUP FROM #TEMP WHERE G_MODELNO = G_MODELNO_GROUP)
			
			UPDATE GOODS
			SET G_FLAG = '1', G_MODELNO_GROUP = NULL, G_CONDITION_NUMBER = NULL, G_GROUP_ATTRIBUTE_ID = NULL, G_GROUP_FLAG = NULL, G_CATEGORY = @NEW_CATE
			WHERE G_MODELNO IN (SELECT G_MODELNO FROM #TEMP)
			
			SET @AFF_ROWS = @@ROWCOUNT
		END
	ELSE
		BEGIN
			UPDATE GOODS
			SET G_FLAG = '1', G_CATEGORY = @NEW_CATE
			WHERE G_CATEGORY = @PRE_CATE
			
			SET @AFF_ROWS = @@ROWCOUNT
		END
	
	IF (@AFF_ROWS > 0)
		BEGIN
			-- 분류변경 로그
			INSERT INTO LOGDB.DBO.TBL_CATE_CHG_LOG (DEL_CATE, NEW_CATE, MM_ID, MM_NM, CHG_FLAG, REGDATE)
			SELECT @PRE_CATE, CASE WHEN @NEW_CATE = '' THEN @PRE_CATE ELSE @NEW_CATE END, @MM_ID, @MM_NM, CASE WHEN @NEW_CATE = '' THEN '2' ELSE '0' END, GETDATE()
		END
END
